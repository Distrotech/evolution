# Process this file with autoconf to produce a configure script.
AC_INIT(camel)
AM_CONFIG_HEADER(config.h)

cflags_set=${CFLAGS+set}

EVOLUTION_MAJOR_VERSION=0
EVOLUTION_MINOR_VERSION=0
EVOLUTION_MICRO_VERSION=1
VERSION=$EVOLUTION_MAJOR_VERSION.$EVOLUTION_MINOR_VERSION.$EVOLUTION_MICRO_VERSION
PACKAGE=evolution

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AC_SUBST(VERSION)


dnl Initialize libtool
AM_PROG_LIBTOOL

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

AC_CANONICAL_HOST

AM_ACLOCAL_INCLUDE(macros)
GNOME_INIT
GNOME_COMPILE_WARNINGS
AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CPP
AC_STDC_HEADERS
AC_ARG_PROGRAM
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

ALL_LINGUAS=""

AM_GNOME_GETTEXT

GNOME_X_CHECKS

dnl **************************************************
dnl * Posix thread support
dnl **************************************************
GNOME_PTHREAD_CHECK
if test "x$PTHREAD_LIB" = "x" ; then
   have_pthread=false
else
   have_pthread=true
fi

AM_CONDITIONAL(HAVE_PTHREAD, $have_pthread)


dnl **************************************************
dnl * ORBit support
dnl **************************************************
dnl GNOMEGNORBA_LIBS="$GNOMEGNORBA_LIBS"
dnl AC_SUBST(GNOMEGNORBA_LIBS)


dnl ******************************
dnl Check for Bonobo
dnl ******************************
AC_MSG_CHECKING(for Bonobo > 0.4)
if gnome-config --libs bonobo > /dev/null 2>&1; then
	vers=`gnome-config --modversion bonobo`
	case $vers
	in
	    bonobo-0.[[01234]]) bonobo_ok=false ;;
	    *) bonobo_ok=true ;;
	esac
else
	bonobo_ok=false
fi

if $bonobo_ok; then
	AC_MSG_RESULT(found)
else
	AC_MSG_ERROR(Bonobo 0.5 is required to compile Evolution)
fi


dnl ******************************
dnl LibGlade checking
dnl ******************************
AC_MSG_CHECKING(For Glade libraries)
if gnome-config --libs libglade > /dev/null 2>&1; then 
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find libGlade installed)
fi

dnl ******************************
dnl GdkPixbuf checking
dnl ******************************
AC_MSG_CHECKING(For Gdkpixbuf libraries)
if gnome-config --libs gdk_pixbuf > /dev/null 2>&1; then 
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find gdkpixbuf installed)
fi

EXTRA_GNOME_LIBS="`gnome-config --libs gnomeui libglade gdk_pixbuf `"
EXTRA_GNOME_CFLAGS="`gnome-config --cflags gnomeui libglade gdk_pixbuf `"
AC_SUBST(EXTRA_GNOME_LIBS)
AC_SUBST(EXTRA_GNOME_CFLAGS)

BONOBO_GNOME_LIBS="`gnome-config --libs gnomeui bonobo libglade gdk_pixbuf `"
BONOBO_GNOME_CFLAGS="`gnome-config --cflags gnomeui bonobo libglade gdk_pixbuf `"
AC_SUBST(BONOBO_GNOME_LIBS)
AC_SUBST(BONOBO_GNOME_CFLAGS)

EXTRA_GNOME_LIBS_THREADS="`gnome-config --libs gnomeui libglade gdk_pixbuf ` `glib-config --libs gthread`"
EXTRA_GNOME_CFLAGS_THREADS="`gnome-config --cflags gnomeui libglade gdk_pixbuf ` `glib-config --cflags gthread`"
AC_SUBST(EXTRA_GNOME_LIBS_THREADS)
AC_SUBST(EXTRA_GNOME_CFLAGS_THREADS)


AC_ARG_WITH(camel-hard-log-level, [  --with-camel-hard-log-level=level  value of log level in camel (0-10)],
            camel_hard_log_level="$withval", camel_hard_log_level="0")
AC_DEFINE_UNQUOTED(CAMEL_HARD_LOG_LEVEL, $camel_hard_log_level)
	
AC_OUTPUT([
Makefile
macros/Makefile
po/Makefile.in
e-util/Makefile
camel/Makefile
camel/providers/Makefile
camel/providers/MH/Makefile
camel/providers/maildir/Makefile
camel/providers/mbox/Makefile
composer/Makefile
devel-docs/Makefile
devel-docs/camel/Makefile
tests/Makefile
tests/ui-tests/Makefile
widgets/Makefile
widgets/meeting-time-sel/Makefile
widgets/shortcut-bar/Makefile
widgets/e-table/Makefile
widgets/toolbar/Makefile
shell/Makefile
])

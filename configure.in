# Process this file with autoconf to produce a configure script.
AC_INIT(camel)
AM_CONFIG_HEADER(config.h)

cflags_set=${CFLAGS+set}

EVOLUTION_MAJOR_VERSION=0
EVOLUTION_MINOR_VERSION=0
EVOLUTION_MICRO_VERSION=1
VERSION=$EVOLUTION_MAJOR_VERSION.$EVOLUTION_MINOR_VERSION.$EVOLUTION_MICRO_VERSION
PACKAGE=evolution

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AC_SUBST(VERSION)


dnl Initialize libtool
AM_PROG_LIBTOOL

dnl Initialize maintainer mode
AM_MAINTAINER_MODE

AC_CANONICAL_HOST

AM_ACLOCAL_INCLUDE(macros)
GNOME_INIT(capplet)
GNOME_COMPILE_WARNINGS
AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CPP
AM_PROG_LEX
AC_PROG_YACC
AC_STDC_HEADERS
AC_ARG_PROGRAM
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

AC_PATH_PROG(SENDMAIL, sendmail, /usr/sbin/sendmail, /usr/sbin:/usr/lib)
AC_DEFINE_UNQUOTED(SENDMAIL_PATH, "$SENDMAIL")

AC_MSG_CHECKING(system mail directory)
if test -d /var/mail; then
	system_mail_dir=/var/mail
else
	system_mail_dir=/var/spool/mail
fi
AC_MSG_RESULT($system_mail_dir)
AC_DEFINE_UNQUOTED(SYSTEM_MAIL_DIR, "$system_mail_dir")


ALL_LINGUAS="el gl no sv uk"

AM_GNOME_GETTEXT

GNOME_X_CHECKS


dnl **************************************************
dnl * pas-backend-file stuff.
dnl * check for db_185.h.  if it's there, we use it.
dnl * otherwise, we use db.h (since it'll be 185).
dnl **************************************************
AC_CHECK_HEADERS(db_185.h)

dnl **************************************************
dnl * Posix thread support
dnl **************************************************

have_pthread=false

AC_ARG_WITH(threads, [--with-threads include threads support],
            [GNOME_PTHREAD_CHECK])

if test "x$PTHREAD_LIB" = "x" ; then
   have_pthread=false
else
   have_pthread=true
fi

AM_CONDITIONAL(ENABLE_THREADS, $have_pthread)


dnl **************************************************
dnl * Print check
dnl **************************************************
AC_MSG_CHECKING(for GnomePrint libraries >= 0.10)
if gnome-config --libs print > /dev/null 2>&1; then 
    vers=`gnome-config --modversion print | sed -e "s/gnome-print-//" | \
        awk 'BEGIN { FS = "."; } { print $1 * 1000 + $2;}'`
    if test "$vers" -ge 10; then
        AC_MSG_RESULT(found)
    else
        AC_MSG_ERROR(You need at least GNOME print 0.10 for this version of Gnumeric)
    fi
else
    AC_MSG_ERROR(Did not find GnomePrint installed)
fi
GNOME_PRINT_LIBS=`gnome-config --libs print`
GNOME_PRINT_CFLAGS=`gnome-config --cflags print`
AC_SUBST(GNOME_PRINT_LIBS)
AC_SUBST(GNOME_PRINT_CFLAGS)

dnl **************************************************
dnl * ORBit support
dnl **************************************************
dnl GNOMEGNORBA_LIBS="$GNOMEGNORBA_LIBS"
dnl AC_SUBST(GNOMEGNORBA_LIBS)


dnl ******************************
dnl Check for Bonobo
dnl ******************************
AC_MSG_CHECKING(for Bonobo > 0.5)
if gnome-config --libs bonobo > /dev/null 2>&1; then
	vers=`gnome-config --modversion bonobo`
	case $vers
	in
	    bonobo-0.[[012345]]) bonobo_ok=false ;;
	    *) bonobo_ok=true ;;
	esac
else
	bonobo_ok=false
fi

if $bonobo_ok; then
	AC_MSG_RESULT(found)
else
	AC_MSG_ERROR(Bonobo 0.5 is required to compile Evolution)
fi


dnl ******************************
dnl LibGlade checking
dnl ******************************
AC_MSG_CHECKING(For Glade libraries)
if gnome-config --libs libglade > /dev/null 2>&1; then 
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find libGlade installed)
fi

dnl ******************************
dnl libunicode checking
dnl ******************************
AC_MSG_CHECKING(For libunicode)
if unicode-config --libs > /dev/null 2>&1; then 
    UNICODE_LIBS=`unicode-config --libs`
    UNICODE_CFLAGS=`unicode-config --cflags`
    AC_SUBST(UNICODE_LIBS)
    AC_SUBST(UNICODE_CFLAGS)
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(libunicode not found)
fi

dnl ******************************
dnl GdkPixbuf checking
dnl ******************************
AC_MSG_CHECKING(For Gdkpixbuf libraries)
if gnome-config --libs gdk_pixbuf gnomecanvaspixbuf > /dev/null 2>&1; then 
    AC_MSG_RESULT(found)
else
    AC_MSG_ERROR(Did not find gdkpixbuf installed)
fi

if $have_pthread; then 
 	THREADS_LIBS="$PTHREAD_LIB `glib-config --libs gthread`"
	THREADS_CFLAGS="`glib-config --cflags gthread`"
else
	THREADS_LIBS=""
	THREADS_CFLAGS=""
fi

GLIB_CFLAGS="`glib-config --cflags`"
GLIB_LIBS="`glib-config --libs`"

AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)


EXTRA_GNOME_LIBS="`gnome-config --libs libglade gdk_pixbuf gnomecanvaspixbuf gnomeui ` $THREADS_LIBS `unicode-config --libs`"
EXTRA_GNOME_CFLAGS="`gnome-config --cflags libglade gdk_pixbuf gnomecanvaspixbuf gnomeui ` $THREADS_CFLAGS `unicode-config --cflags`"

AC_SUBST(EXTRA_GNOME_LIBS)
AC_SUBST(EXTRA_GNOME_CFLAGS)


BONOBO_GNOME_LIBS="`gnome-config --libs bonobo libglade gdk_pixbuf gnomecanvaspixbuf gnomeui`"
BONOBO_GNOME_CFLAGS="`gnome-config --cflags bonobo libglade gdk_pixbuf gnomecanvaspixbuf gnomeui `"
AC_SUBST(BONOBO_GNOME_LIBS)
AC_SUBST(BONOBO_GNOME_CFLAGS)

BONOBO_HTML_GNOME_LIBS="`gnome-config --libs bonobo libglade gdk_pixbuf gtkhtml gnomecanvaspixbuf gnomeui ` `glib-config --libs gthread`"
BONOBO_HTML_GNOME_CFLAGS="`gnome-config --cflags bonobo libglade gdk_pixbuf gtkhtml gnomecanvaspixbuf gnomeui ` `glib-config --cflags gthread`"
AC_SUBST(BONOBO_HTML_GNOME_LIBS)
AC_SUBST(BONOBO_HTML_GNOME_CFLAGS)

dnl ******************************
dnl GtkHTML checking
dnl ******************************

AC_MSG_CHECKING(for GtkHTML)
if gnome-config --libs gtkhtml > /dev/null 2>&1; then
	gtkhtml_ok=true
else
	gtkhtml_ok=false
fi

if $gtkhtml_ok; then
	AC_MSG_RESULT(found)
else
	AC_MSG_ERROR(GtkHTML is required to compile Evolution)
fi

GTKHTML_CFLAGS="`gnome-config --cflags gtkhtml`"
GTKHTML_LIBS="`gnome-config --libs gtkhtml`"

AC_SUBST(GTKHTML_CFLAGS)
AC_SUBST(GTKHTML_LIBS)

dnl ******************************
dnl Gnome-VFS checking
dnl ******************************
AC_MSG_CHECKING(for GNOME-VFS)
if gnome-config --libs vfs > /dev/null 2>&1; then
	vfs_ok=true
else
	vfs_ok=false
fi

if $vfs_ok; then
	AC_MSG_RESULT(found)
else
	AC_MSG_ERROR(GNOME-VFS is required to compile Evolution)
fi

BONOBO_VFS_GNOME_LIBS="`gnome-config --libs bonobo libglade gdk_pixbuf gnomecanvaspixbuf vfs gnomeui `"
BONOBO_VFS_GNOME_CFLAGS="`gnome-config --cflags bonobo libglade gdk_pixbuf gnomecanvaspixbuf vfs gnomeui `"
AC_SUBST(BONOBO_VFS_GNOME_LIBS)
AC_SUBST(BONOBO_VFS_GNOME_CFLAGS)

dnl ******************************
dnl Pilot checking
dnl ******************************
GNOME_PILOT_CHECK


AC_ARG_WITH(camel-hard-log-level, [  --with-camel-hard-log-level=level  value of log level in camel (0-10)],
            camel_hard_log_level="$withval", camel_hard_log_level="0")
AC_DEFINE_UNQUOTED(CAMEL_HARD_LOG_LEVEL, $camel_hard_log_level)

AC_CONFIG_SUBDIRS(libical)

AC_SUBST(CAPPLET_LIBS)
	
AC_OUTPUT([
Makefile
macros/Makefile
intl/Makefile
po/Makefile.in
e-util/Makefile
doc/Makefile
libibex/Makefile
camel/Makefile
camel/providers/Makefile
camel/providers/MH/Makefile
camel/providers/maildir/Makefile
camel/providers/mbox/Makefile
camel/providers/pop3/Makefile
camel/providers/sendmail/Makefile
camel/providers/smtp/Makefile
composer/Makefile
devel-docs/Makefile
devel-docs/camel/Makefile
tests/Makefile
tests/ui-tests/Makefile
widgets/Makefile
widgets/e-text/Makefile
widgets/meeting-time-sel/Makefile
widgets/shortcut-bar/Makefile
widgets/e-table/Makefile
addressbook/Makefile
addressbook/contact-editor/Makefile
addressbook/printing/Makefile
addressbook/backend/Makefile
addressbook/backend/idl/Makefile
addressbook/backend/pas/Makefile
addressbook/backend/ebook/Makefile
addressbook/gui/Makefile
addressbook/gui/minicard/Makefile
addressbook/gui/component/Makefile
shell/Makefile
mail/Makefile
data/Makefile
libversit/Makefile
libibex/Makefile
calendar/Makefile
calendar/doc/Makefile
calendar/doc/C/Makefile
calendar/idl/Makefile
calendar/cal-util/Makefile
calendar/cal-client/Makefile
calendar/pcs/Makefile
calendar/gui/Makefile
filter/Makefile
wombat/Makefile
art/Makefile
])

/*-*- Mode: IDL; tab-width: 8; indent-tabs-mode: t; c-basic-offset: 8 -*- */
/*
 * CORBA interface for the Evolution Executive Summary Components.
 *
 * Authors:
 *   Iain Holmes  <iain@helixcode.com> 
 *
 * Copyright (C) 2000 Helix Code, Inc.
 */

/* TODO: Needs Exceptions */

#include <Bonobo.idl>

module Evolution {
	interface Summary;

	interface SummaryComponent: Bonobo::Unknown {

		/**
		 * set_owner:
		 * @summary: A Summary object.
		 *
		 * Sets the owner of the component.
		 */
		void set_owner (in Summary owner);

		/**
		 * unset_owner:
		 *
		 * Unsets the owner of the component.
		 */
		void unset_owner ();

		/**
		 * create_view:
		 * @control: A Bonobo_Control
		 * @html: HTML of the view.
		 * @title: Title of the view.
		 * @icon: Icon to be used for the view.
	 	 *
		 * Creates a summary of the data that the component has.
		 * Returns: The ID of the view.
		 */
		long create_view (out Bonobo::Control control,
		                  out string html,
				  out string title,
			          out string icon);

		/**
		 * destroy_view:
		 * @id: ID of the view to be destroyed.
		 *
		 * Tells a service that a view has been destroyed.
		 */
		void destroy_view (in long id);

		/**
		 * configure:
		 * 
		 * Instructs the component to open a preferences dialog.
		 */
		void configure ();

	};
};

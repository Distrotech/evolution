/* -*- Mode: IDL; tab-width: 8; indent-tabs-mode: t; c-basic-offset: 8 -*- */
/*
 * Storage interface for the Evolution shell.
 *
 * Authors:
 *   Ettore Perazzoli <ettore@ximian.com>
 *
 * Copyright (C) 2000, 2001 Ximian, Inc.
 */

#include <Bonobo.h>

module GNOME {
module Evolution {
	interface Storage;
	interface StorageListener;

	interface Storage : Bonobo::Unknown {
		exception AlreadyListening {};
		exception NotFound {};

		attribute string name;

		enum Result {
			OK,
			UNSUPPORTED_OPERATION,
			UNSUPPORTED_TYPE,
			INVALID_URI,
			ALREADY_EXISTS,
			DOES_NOT_EXIST,
			PERMISSION_DENIED,
			NO_SPACE,
			NOT_EMPTY,
			GENERIC_ERROR
		};

		struct FolderResult {
			Result result;
			string path;
		};

		void asyncCreateFolder (in string path,
					in string type,
					in string description,
					in string parent_physical_uri,
					in Bonobo::Listener listener);

		void asyncRemoveFolder (in string path,
					in string physical_uri,
					in Bonobo::Listener listener);

		void asyncXferFolder (in string source_path,
				      in string destination_path,
				      in boolean remove_source,
				      in Bonobo::Listener listener);

		void updateFolder (in string path,
				   in long unread_count);

		void addListener       (in StorageListener listener)
			raises (AlreadyListening);

		void removeListener    (in StorageListener listener)
			raises (NotFound);
	};

	interface StorageListener {
		exception Exists {};
		exception NotFound {};

		void notifyDestroyed ();

		/* FIXME exceptions don't make much sense here...  */

		void notifyFolderCreated (in string  path,
					  in Folder  folder)
			raises (Exists);
		
		void notifyFolderUpdated (in string  path,
					  in long    unread_count)
			raises (NotFound);

		void notifyFolderRemoved (in string  path)
			raises (NotFound);
	};

	interface StorageRegistry : Bonobo::Unknown {
		exception Exists {};
		exception NotFound {};

		/* FIXME: The toplevel attributes should probably be attributes
                   of the storage instead of being passed here.  */
		StorageListener addStorage (in Storage storage,
					    in string name,
					    in string toplevel_node_uri,
					    in string toplevel_node_type)
			raises (Exists);

		void removeStorageByName (in string name)
			raises (NotFound);
	};
};
};

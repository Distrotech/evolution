/* -*- Mode: IDL; tab-width: 8; indent-tabs-mode: t; c-basic-offset: 8 -*- */
/*
 * Storage interface for the Evolution shell.
 *
 * Authors:
 *   Ettore Perazzoli <ettore@ximian.com>
 *
 * Copyright (C) 2000, 2001 Ximian, Inc.
 */

#include <Bonobo.h>

module GNOME {
module Evolution {
	interface Storage;
	interface StorageListener;

	interface Storage : Bonobo::Unknown {
		exception AlreadyListening {};
		exception NotFound {};

		attribute string name;

		enum Result {
			OK,
			UNSUPPORTED_OPERATION,
			UNSUPPORTED_TYPE,
			INVALID_URI,
			ALREADY_EXISTS,
			DOES_NOT_EXIST,
			PERMISSION_DENIED,
			NO_SPACE,
			NOT_EMPTY,
			GENERIC_ERROR
		};

		struct FolderResult {
			Result result;
			string path;
		};

		void asyncCreateFolder (in string path,
					in string type,
					in string description,
					in string parent_physical_uri,
					in Bonobo::Listener listener);

		void asyncRemoveFolder (in string path,
					in string physical_uri,
					in Bonobo::Listener listener);

		void asyncXferFolder (in string source_path,
				      in string destination_path,
				      in boolean remove_source,
				      in Bonobo::Listener listener);

		void asyncOpenFolder (in string path);

		void updateFolder (in string path,
				   in long unread_count);

		void addListener       (in StorageListener listener)
			raises (AlreadyListening);

		void removeListener    (in StorageListener listener)
			raises (NotFound);
	};

	interface StorageListener {
		exception Exists {};
		exception NotFound {};

		void notifyDestroyed ();

		/* FIXME exceptions don't make much sense here...  */

		void notifyFolderCreated (in string path,
					  in Folder folder)
			raises (Exists);
		
		void notifyFolderUpdated (in string path,
					  in long   unread_count)
			raises (NotFound);

		void notifyFolderRemoved (in string path)
			raises (NotFound);

		void notifyHasSubfolders (in string path,
					  in string message)
			raises (NotFound);
	};

	interface StorageRegistry : Bonobo::Unknown {
		exception Exists {};
		exception NotFound {};
		exception AlreadyListening {};

		enum MessageType {
			STORAGE_CREATED,
			STORAGE_DESTROYED
		};

		struct NotifyResult {
			MessageType type;
			string name;
		};
		
		StorageListener addStorage (in Storage storage,
					    in string name)
			raises (Exists);
		
		Storage getStorageByName (in string name)
			raises (NotFound);

		void removeStorageByName (in string name)
			raises (NotFound);

		void addListener (in Bonobo::Listener listener)
			raises (AlreadyListening);

		void removeListener (in Bonobo::Listener listener)
			raises (NotFound);

		Folder getFolderByUri (in string uri)
			raises (NotFound);
	};
};
};
